/// This class consists of three ClassMethods to get input of free text, generate embeddings from the input text leveraging
/// 'sentence_transformers' library, and insert the vectors generated by the embedding model into a table, respectively.
Class vectors.vectorstore Extends %RegisteredObject
{

// Get text and generate embeddings.

// This is written with Embedded Python, as an embedding model is required to transform text into embeddings.

ClassMethod GetEmbeddings(sentence) [ Language = python ]
{
    import json
    # import the embedding model
    import sentence_transformers

    # create the model and form the embeddings
    model = sentence_transformers.SentenceTransformer('all-MiniLM-L6-v2')
    embeddings = model.encode(sentence)

    embeddings_list = embeddings.tolist()
    embeddings_json = json.dumps(embeddings_list)
    return embeddings_json
}

// This ClassMethod reads text data from a file and calls the ClassMethod GetEmbeddings() (supplying the free text as parameter) and

// when embeddings are returned, this then uses dynamic SQL to insert the text and corresponding embeddings into a persistent class.

ClassMethod InsertEmbeddings() As %Status
{
    Try {
        // Initialize the file object
        Set file = ##class(%File).%New("C:\Users\padhikar\OneDrive - InterSystems Corporation\code\vectorsearch\vectors\text.txt")
        Set sc = file.Open("R")
        If $$$ISERR(sc) {
            Throw ##class(%Exception.General).Create("Error opening file: "_$SYSTEM.Status.GetErrorText(sc))
        }

        // Loop through each line in the file
        While 'file.AtEnd {
            Set sentence = file.ReadLine()
            Set embeddings = ..GetEmbeddings(sentence)

            // Remove the square brackets from the string
            Set embeddings = $EXTRACT(embeddings, 2, $LENGTH(embeddings) - 1)

            // Prepare SQL query
            Set query = "INSERT INTO vectors.Embeddings (text, vectors) VALUES (?, TO_VECTOR(?,double , 800))"
            Set statement = ##class(%SQL.Statement).%New()
            $$$ThrowOnError(statement.%Prepare(query))

            // Execute SQL query
            Set rset = statement.%Execute(sentence, embeddings)
            If rset.%SQLCODE < 0 {
                Throw ##class(%Exception.SQL).CreateFromSQLCODE(rset.%SQLCODE, rset.%Message)
            }
        }

        // Close the file
        Do file.Close()

        // Return success status
        Return $$$OK
    } Catch ex {
        // Handle exceptions
        Write "An error occurred: ", ex.DisplayString(), !
        // Ensure file is closed in case of error
        If ($IsObject(file) && file.IsOpen()) {
            Do file.Close()
        }
        // Return error status
        Return ex.AsStatus()
    }
}


// This method takes a query or prompt as an input from user, transforms it into embeddings by passing it to the ClassMethod GetEmbeddings()

// and when returned, it performs a search on vectors and returns the texts that have matching vectors.

ClassMethod VectorSearch(searchTerms) As %Status
{
    Try {
        // Prepare the SQL query
        Set query = "SELECT TOP 3 * FROM vectors.Embeddings ORDER BY VECTOR_DOT_PRODUCT(vectors, TO_VECTOR(?, double, 800)) DESC"
        Set statement = ##class(%SQL.Statement).%New()
        $$$ThrowOnError(statement.%Prepare(query))

        // Convert input string to an embedding
        Set searchTermsEmbedding = ..GetEmbeddings(searchTerms)
        // Remove the square brackets from the string
        Set searchTermsEmbedding = $EXTRACT(searchTermsEmbedding, 2, $LENGTH(searchTermsEmbedding) - 1)

        // Execute the SQL query
        Set rset = statement.%Execute(searchTermsEmbedding)
        If rset.%SQLCODE < 0 {
            Throw ##class(%Exception.SQL).CreateFromSQLCODE(rset.%SQLCODE, rset.%Message)
        }

        // Process retrieved descriptions and return the result
        Set lineNumber = 1
        Set retrievedInfo = ""
        While rset.%Next() {
            // Concatenate a newline character to the end of each retrieved info string
            Set retrievedInfo = retrievedInfo_lineNumber_". "_rset.%Get("text")_$CHAR(13,10)
            Set lineNumber = lineNumber + 1
        }
        
        // Output the retrieved information
        Write retrievedInfo
        
        // Return success status
        Return $$$OK
    } Catch ex {
        // Handle exceptions
        Write "An error occurred: ", ex.DisplayString(), !
        // Return error status
        Return ex.AsStatus()
    }
}


}
